# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh-osk-stevia
pkgver=0.48.0
pkgrel=1
pkgdesc="On-screen keyboard for Phosh"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
  'feedbackd'
  'gnome-desktop'
  'gtk3'
  'hunspell'
  'json-glib'
  'libgmobile'
  'libhandy'
  'wayland'
)
makedepends=(
  'gi-docgen'
  'glib2-devel'
  'glslang'
  'meson'
  'python-docutils'
  'wayland-protocols'
)
checkdepends=('xorg-server-xvfb')
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('b0a9a93bb0ebaf990a2dbf99391744cb2f9d3370cdf9eee69040c8bff59795a37dc03b1d847308cbd15542a7941dfed5618670b5b08c8fb9468eb5e62abe6bc9'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
  local meson_options=(
    -D gtk_doc=true
    -D man=true
  )

  arch-meson $pkgname-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  LC_ALL=C.UTF-8 xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
